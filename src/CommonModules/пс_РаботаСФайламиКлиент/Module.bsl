
#Область ПрограммныйИнтерфейс

Процедура ВыбратьФайлы(ОбработчикРезультата, ИдентификаторФормы
	, Фильтр = "", Заголовок = Неопределено, МножественныйВыбор = Ложь) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИдентификаторФормы",   	ИдентификаторФормы);
	Параметры.Вставить("Фильтр",               	Фильтр);
	Параметры.Вставить("Заголовок",             Заголовок);
	Параметры.Вставить("МножественныйВыбор",	МножественныйВыбор);
	Параметры.Вставить("ОбработчикРезультата", 	ОбработчикРезультата);
	
	Если ЭтоПлатформа15Плюс() Тогда
		ВыбратьФайлыПлатформа15Плюс(Параметры); 
	Иначе
		ВыбратьФайлыПлатформа15Минус(Параметры);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Платформа15Плюс

Процедура ВыбратьФайлыПлатформа15Плюс(Параметры)
	ОписаниеОповещенияОЗавершении
		= Параметры.ОбработчикРезультата;
	ОписаниеОповещенияОХодеВыполнения 
		= Новый ОписаниеОповещения("ОповещениеОХодеВыполненияПомещенияФайлов", ЭтотОбъект);
	ОписаниеОповещенияПередНачалом 
		= Новый ОписаниеОповещения("ПередНачаломПомещенияФайлов", ЭтотОбъект);
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов();
	ПараметрыДиалога.МножественныйВыбор = Параметры.МножественныйВыбор;
	ПараметрыДиалога.Фильтр = Параметры.Фильтр;
	ПараметрыДиалога.Заголовок = Параметры.Заголовок;
	ИДФормы = Параметры.ИдентификаторФормы;
	// чтобы модуль скомпилировался в платформе менее 15
	Команда = "НачатьПомещениеФайловНаСервер(ОписаниеОповещенияОЗавершении
		|, ОписаниеОповещенияОХодеВыполнения, ОписаниеОповещенияПередНачалом
		|, ПараметрыДиалога, ИДФормы)";
	Выполнить(Команда);
КонецПроцедуры

Процедура ОповещениеОХодеВыполненияПомещенияФайлов(ПомещаемыйФайл, Помещено
		, ОтказОтПомещенияФайла, ПомещеноВсего
		, ОтказОтПомещенияВсехФайлов, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

Процедура ПередНачаломПомещенияФайлов(ПомещаемыеФайлы, ОтказОтПомещенияВсехФайлов
	, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область Платформа15Минус

Процедура ВыбратьФайлыПлатформа15Минус(Параметры)
	Описание = Новый ОписаниеОповещения("ВыбратьФайлыПослеУстановкиРасширение", ЭтотОбъект, Параметры);
	ПоказатьВопросУстановкиРасширенияРаботыСФайлами(Описание);	
КонецПроцедуры

Процедура ПоказатьВопросУстановкиРасширенияРаботыСФайлами(ОписаниеОповещения)
	Если Не КлиентПоддерживаетСинхронныеВызовы() Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Ложь);
		Возврат;
	КонецЕсли;
	#Если Не ВебКлиент Тогда
	// В мобильном, тонком и толстом клиентах расширение подключено всегда.
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, Истина);
	Возврат;
	#КонецЕсли
	
	// TODO: сделать поддержку вебклиента	
	
КонецПроцедуры

Функция КлиентПоддерживаетСинхронныеВызовы()
	
#Если ВебКлиент Тогда
	// В Chrome и Firefox синхронные методы не поддерживаются.
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ИнформацияПрограммыМассив = СтрРазделить(СистемнаяИнформация.ИнформацияПрограммыПросмотра, " ", Ложь);
	
	Для Каждого ИнформацияПрограммы Из ИнформацияПрограммыМассив Цикл
		Если СтрНайти(ИнформацияПрограммы, "Chrome") > 0 ИЛИ СтрНайти(ИнформацияПрограммы, "Firefox") > 0 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
#КонецЕсли
	
	Возврат Истина;
	
КонецФункции

Процедура ВыбратьФайлыПослеУстановкиРасширение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		Возврат;	
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ПоместитьФайлыНачало", ЭтотОбъект, ДополнительныеПараметры);
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = ДополнительныеПараметры.Фильтр;
	Диалог.МножественныйВыбор = ДополнительныеПараметры.МножественныйВыбор;
	Если ДополнительныеПараметры.Свойство("ЗаголовокДиалога") Тогда
		Диалог.Заголовок = ДополнительныеПараметры.ЗаголовокДиалога;
	КонецЕсли;
	Диалог.Показать(Описание);
	
КонецПроцедуры

Процедура ПоместитьФайлыНачало(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПомещаемыеФайлы = Новый Массив;
	Для Каждого текВыбранныйФайл Из ВыбранныеФайлы Цикл
		ПомещаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(текВыбранныйФайл));	
	КонецЦикла;
	Описание = Новый ОписаниеОповещения("ПоместитьФайлыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	НачатьПомещениеФайлов(Описание, ПомещаемыеФайлы,,, ДополнительныеПараметры.ИдентификаторФормы);	
КонецПроцедуры

Процедура ПоместитьФайлыЗавершение(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	// приведём параметры к виду ОписаниеПомещенногоФайла, добавленный в 15 версии 
	РезультатПомещённыеФайлы = Новый Массив;
	Для Каждого текПомещённыйФайл Из ПомещенныеФайлы Цикл
		Файл = Новый Файл(текПомещённыйФайл.ПолноеИмя);
		СсылкаНаФайл = Новый Структура;
		СсылкаНаФайл.Вставить("ИдентификаторФайла", текПомещённыйФайл.ИдентификаторФайла);
		СсылкаНаФайл.Вставить("Имя",				текПомещённыйФайл.Имя);
		СсылкаНаФайл.Вставить("Расширение",			Файл.Расширение);
		СсылкаНаФайл.Вставить("Файл",				Файл);
		ПомещённыйФайл = Новый Структура;
		ПомещённыйФайл.Вставить("Адрес", текПомещённыйФайл.Хранение);
		ПомещённыйФайл.Вставить("ПомещениеФайлаОтменено", Ложь);
		ПомещённыйФайл.Вставить("СсылкаНаФайл", СсылкаНаФайл);
		РезультатПомещённыеФайлы.Добавить(ПомещённыйФайл);		
	КонецЦикла;	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикРезультата, РезультатПомещённыеФайлы);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункции

Функция ЭтоПлатформа15Плюс()
	СистемнаяИнформация = Новый СистемнаяИнформация();
	МассивВерсий = СтрРазделить(СистемнаяИнформация.ВерсияПриложения, ".");
	Мажорная = МассивВерсий.Получить(1);
	Минорная = МассивВерсий.ПОлучить(2);
	Возврат Мажорная = "3" И Число(Минорная) >= 15;
КонецФункции

#КонецОбласти







